plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.192'
}

version = mod_version
group = "com.eastonium.nuicraft"

base {
    archivesName = "nuicraft"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        arguments.add '--nogui'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    maven { name = "Jared's maven"; url = "https://maven.blamejared.com/" }
    maven { name = "ModMaven"; url = "https://modmaven.dev" }
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    // AzureLib for 3D mask model (geo/armor/mask.geo.json). Build AzureLib first: cd AzureLib && ./gradlew :neo:build
    implementation files("${rootDir}/AzureLib/neo/build/libs/azurelib-neo-1.21.8-3.2.0.jar")
    // JEI (optional) - for recipe/item list integration. Use 1.21 artifact for compatibility.
    compileOnly "mezz.jei:jei-1.21-neoforge-api:19.5.0.59"
    runtimeOnly "mezz.jei:jei-1.21-neoforge:19.5.0.59"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}
